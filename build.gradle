apply plugin: 'java'

repositories {
	mavenCentral()
	flatDir {
		dirs 'lib'
	}
}

jar {
	from {
		configurations.extraLibs.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes(
			'Main-Class': 'org.betacraft.launcher.Launcher'
		)
	}
}

task osxBuildApp {
	dependsOn jar

	doLast {
		def dist = "${buildDir}/app/BetaCraft Launcher.app"
		mkdir("$dist/Contents/MacOS")
		mkdir("$dist/Contents/Resources")
		copy {
			from "${buildDir}/../src/main/app/Info.plist"
			into "$dist/Contents"
		}
		copy {
			from "${buildDir}/../src/main/app/PkgInfo"
			into "$dist/Contents"
		}
		copy {
			from "${buildDir}/../src/main/app/BetaCraft Launcher"
			into "$dist/Contents/MacOS"
		}
		copy {
			from "${buildDir}/../src/main/app/icon.icns"
			into "$dist/Contents/Resources"
		}
		copy {
			from "${buildDir}/libs/${project.name}.jar"
			into "$dist/Contents/MacOS"
		}
		exec {
			workingDir "${buildDir}/app/"
			commandLine('zip', '-rvFSX', "../libs/BetaCraft-Launcher-macOS.zip", '.')
		}
	}
}

build.dependsOn osxBuildApp

configurations {
	extraLibs
}

dependencies {
	implementation 'com.google.code.gson:gson:2.8.6'
	implementation name: 'discord-rpc-1.6.2'
	extraLibs group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
}
